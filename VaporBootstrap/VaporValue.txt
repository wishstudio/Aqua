class Value
{
	public Location location;

	/* HACK: bool */
	public abstract function CanImplicitlyConvertTo(Type type) -> int32;

	public abstract function GetNextFreeRegister(int32 freereg) -> int32;

	public abstract function ToRegister(Compiler compiler, int32 regid) -> RegisterValue;

	public function EnsureRegister(Compiler compiler, int32 freereg) -> RegisterValue
	{
		if (this is RegisterValue)
			return this;
		else if (this is VariableValue)
		{
			VariableValue v = [VariableValue] this;
			return new RegisterValue(location, v::regid, v::type);
		}
		return ToRegister(compiler, freereg);
	}

	/* Ensure a value occupies a single register slot at most */
	public function EnsureSingleSlot(Compiler compiler, int32 freereg) -> TypedValue
	{
		if (this is ImmediateValue)
			return this;
		return EnsureRegister(compiler, freereg);
	}

	/* TODO: super function call */
	/* Ensure value is of given type, convert it otherwise */
	public abstract function ToTyped(Compiler compiler, int32 freereg, Type type) -> TypedValue;

	/* Convert value to a given type and move in to given register */
	public function ToTypedRegister(Compiler compiler, int32 regid, Type type) -> RegisterValue
	{
		return ToTyped(compiler, regid, type)::ToRegister(compiler, regid);
	}

	public function EnsureTypedRegister(Compiler compiler, int32 regid, Type type) -> RegisterValue
	{
		return ToTyped(compiler, regid, type)::EnsureRegister(compiler, regid);
	}

	public function ToLValue(Compiler compiler, LValue lvalue, int32 freereg)
	{
		if (CanImplicitlyConvertTo(lvalue::type) == 0)
			throw new CompilerException(
				"Cannot implicitly convert to \"" + lvalue::type::ToString() + "\"",
				location
			);
		lvalue::AssignValue(compiler, this, freereg);
	}

	/* HACK: bool */
	public virtual function ToJumpTarget(Compiler compiler, int32 freereg, int32 jtarget, int32 jtrue)
		-> JumpTargetValue
	{
		throw new CompilerException("Logical value expected.", location);
	}
}

class PseudoValue: Value
{
	public override function CanImplicitlyConvertTo(Type type) -> int32
	{
		throw new Core.InvalidOperationException();
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		throw new Core.InvalidOperationException();
	}

	public override function ToRegister(Compiler compiler, int32 regid) -> RegisterValue
	{
		throw new Core.InvalidOperationException();
	}

	public override function ToTyped(Compiler compiler, int32 freereg, Type type) -> TypedValue
	{
		throw new Core.InvalidOperationException();
	}
}

class TypeValue: PseudoValue
{
	public Type type;

	public function .ctor(Location location, Type type)
	{
		this::location = location;
		this::type = type;
	}
}

class MethodValue: PseudoValue
{
	public int32 regid;
	public Vector methods;

	public function .ctor(Location location, int32 regid, Vector methods)
	{
		this::location = location;
		this::regid = regid;
		this::methods = methods;
	}
}

class TypedValue: Value
{
	Type type;

	public override function CanImplicitlyConvertTo(Type dst) -> int32
	{
		return type::CanImplicitlyConvertTo(dst);
	}

	public override function ToTyped(Compiler compiler, int32 freereg, Type dst) -> TypedValue
	{
		Type stackType = type::.getStackType();
		/* HACK: Temporary workaround */
		if (type is PointerType && dst is IntType)
			return this;
		else if (stackType::Equals(dst) > 0  || type::Equals(dst) > 0)
			return this;
		else if (type is PrimitiveType && dst is PrimitiveType)
		{
			/* TODO: Maybe we could support bool -> [u]int* conversions */
			if (type is BoolValue || dst is BoolType)
				throw new CompilerException("Invalid type conversion", location);
			/* TODO: Maybe we could cut off some unnecessary convertions */
			RegisterValue ret = ToRegister(compiler, freereg);
			compiler::AddCode("conv\t" + dst::ToString() + " $" + freereg + ", "
				+ stackType::ToString() + " $" + ret::regid);
			ret::type = dst;
			return ret;
		}
		/* HACK: not */
		else
		{
			if (type is PrimitiveType || dst is PrimitiveType)
				throw new CompilerException(
					"Cannot convert \"" + type::ToString() + "\" to \"" + dst::ToString() + "\"",
					location
				);

			/* Object cast */
			/* TODO */
			if (type::CanImplicitlyConvertTo(dst) > 0)
			{
				/* FIXME: Clone */
				this::type = type;
				return this;
			}
			else
			{
				RegisterValue ret = ToRegister(compiler, freereg);
				compiler::AddCode("cast\t$" + freereg + ", $" + ret::regid + ", " + dst::ToString());
				ret::type = type;
				return ret;
			}
		}
	}

	public override function ToJumpTarget(Compiler compiler, int32 freereg, int32 jtarget, int32 jtrue)
		-> JumpTargetValue
	{
		RegisterValue value = EnsureTypedRegister(compiler, freereg, Type::Bool);
		Core.String inst;
		if (jtrue > 0) /* jump on true */
			inst = "jnzi";
		else /* jump on false */
			inst = "jzi";
		compiler::AddCode(inst + "\t$" + value::regid + ", " + compiler::GetLabelName(jtarget));
		return new JumpTargetValue(location, jtarget);
	}
}

class ImmediateValue: TypedValue
{
	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		return freereg;
	}
}

/* HACK: bool */
class BoolValue: ImmediateValue
{
	public int32 value;

	public function .ctor(Location location, int32 value)
	{
		this::location = location;
		this::value = value;
		this::type = Type::Bool;
	}

	public override function CanImplicitlyConvertTo(Type type) -> int32
	{
		if (type is BoolType)
			return 1;
		return 0;
	}

	public override function ToRegister(Compiler compiler, int32 regid) -> RegisterValue
	{
		compiler::AddCode("ldi\t$" + regid + ", " + value);
		return new RegisterValue(location, regid, Type::Bool);
	}

	public override function ToTyped(Compiler compiler, int32 freereg, Type type) -> TypedValue
	{
		if (type is BoolType)
			return ToRegister(compiler, freereg);
		else
			throw new CompilerException("Invalid type conversion.", location);
	}
}

class NullValue: ImmediateValue
{
	public function .ctor(Location location)
	{
		this::location = location;
		this::type = Type::Int;
	}

	public override function CanImplicitlyConvertTo(Type type) -> int32
	{
		if (type is IntType || type is ClassType || type is PointerType)
			return 1;
		return 0;
	}

	public override function ToRegister(Compiler compiler, int32 regid) -> RegisterValue
	{
		/* TODO */
		compiler::AddCode("ldnull\t$" + regid);
		return new RegisterValue(location, regid, Type::Int);
	}

	public override function ToTyped(Compiler compiler, int32 freereg, Type type) -> TypedValue
	{
		if (type is IntType || type is ClassType || type is PointerType)
		{
			compiler::AddCode("ldnull\t$" + freereg);
			return new RegisterValue(location, freereg, type);
		}
		else
			throw new CompilerException("Invalid type conversion.", location);
	}
}

class IntegerValue: ImmediateValue
{
	Core.String value;

	public function .ctor(Location location, Core.String value)
	{
		this::location = location;
		/* TODO */
		this::value = value;
		this::type = Type::Int32;
	}

	public override function CanImplicitlyConvertTo(Type type) -> int32
	{
		/* TODO */
		if (type is Int8Type || type is UInt8Type || type is Int16Type || type is UInt16Type
			|| type is Int32Type || type is UInt32Type)
			return 1;
		return 0;
	}

	public override function ToRegister(Compiler compiler, int32 regid) -> RegisterValue
	{
		/* TODO */
		compiler::AddCode("ldi\t$" + regid + ", " + value);
		return new RegisterValue(location, regid, Type::Int32);
	}

	public override function ToTyped(Compiler compiler, int32 freereg, Type type) -> TypedValue
	{
		/* TODO: Super call */
		return this;
	}
}

class JumpTargetValue: TypedValue
{
	public int32 labelId;

	public function .ctor(Location location, int32 labelId)
	{
		this::location = location;
		this::labelId = labelId;
		this::type = Type::Bool;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		/* TODO: Is this okay? */
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 regid) -> RegisterValue
	{
		/* TODO */
		/* Default: jtrue == 0 */
		compiler::AddCode("ldi\t$" + regid + ", 1");
		compiler::AddCode("jmp\t" + compiler::GetLabelName(compiler::currentLabelId));
		compiler::AddLabel(labelId);
		compiler::AddCode("ldi\t$" + regid + ", 0");
		compiler::AddLabel(compiler::GetNextLabelId());
		return new RegisterValue(location, regid, Type::Bool);
	}

	public override function ToJumpTarget(Compiler compiler, int32 freereg, int32 jtarget, int32 jtrue)
		-> JumpTargetValue
	{
		return this;
	}
}

class RegisterValue: TypedValue
{
	public int32 regid;

	public function .ctor(Location location, int32 regid, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid)
			return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
		if (regid == dst)
			return this;
		compiler::AddCode("ld" + type::.getStackType()::.getSuffix() + "\t$" + dst + ", $" + regid);
		return new RegisterValue(location, dst, type);
	}
}

class LValue: TypedValue
{
	/* Assign a value to this lvalue, caller ensures type consistent */
	public virtual function AssignValue(Compiler compiler, Value value, int32 freereg) {}
}

class VariableValue: LValue
{
	public int32 regid;

	public function .ctor(Location location, int32 regid, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid)
			return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
		if (regid == dst)
			return this;
		compiler::AddCode("ld" + type::.getStackType()::.getSuffix() + "\t$" + dst + ", $" + regid);
		return new RegisterValue(location, dst, type);
	}

	public override function AssignValue(Compiler compiler, Value value, int32 freereg)
	{
		value::ToTypedRegister(compiler, regid, type);
	}
}

class DerefValue: LValue
{
	public int32 regid;

	public function .ctor(Location location, int32 regid, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid)
			return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
		compiler::AddCode("ldi" + type::.getSuffix() + "\t$" + dst + ", ($" + regid + ")");
		return new RegisterValue(location, dst, type);
	}

	public override function AssignValue(Compiler compiler, Value value, int32 freereg)
	{
		RegisterValue v = value::EnsureTypedRegister(compiler, freereg, type);
		compiler::AddCode("ldi" + type::.getSuffix() + "\t($" + regid + "), $" + v::regid);
	}
}

class FieldValue: LValue
{
	public int32 regid;
	public Field field;

	public function .ctor(Location location, int32 regid, Field field, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::field = field;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid)
			return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
		if (field::modifier & Modifier::Static > 0)
			compiler::AddCode("lds" + type::.getSuffix() + "\t$" + dst + ", "
				+ field::classObject::name + "::" + field::name);
		else
			compiler::AddCode("ldf" + type::.getSuffix() + "\t$" + dst + ", $"
				+ regid + ", " + field::classObject::name + "::" + field::name);
		return new RegisterValue(location, dst, type);
	}

	public override function AssignValue(Compiler compiler, Value value, int32 freereg)
	{
		RegisterValue v = value::EnsureTypedRegister(compiler, freereg, type);
		if (field::modifier & Modifier::Static > 0)
			compiler::AddCode("lds" + type::.getSuffix() + "\t" + field::classObject::name + "::" + field::name
				+ ", $" + v::regid);
		else
			compiler::AddCode("ldf" + type::.getSuffix() + "\t$" + regid + ", "
				+ field::classObject::name + "::" + field::name + ", $" + v::regid);
	}
}

class PropertyValue: LValue
{
	public int32 regid;
	public Property property;

	public function .ctor(Location location, int32 regid, Property property, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::property = property;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid)
			return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
	}
}

class ArrayElementValue: LValue
{
	public int32 regid, regid2;

	public function .ctor(Location location, int32 regid, int32 regid2, Type type)
	{
		this::location = location;
		this::regid = regid;
		this::regid2 = regid2;
		this::type = type;
	}

	public override function GetNextFreeRegister(int32 freereg) -> int32
	{
		if (freereg == regid || freereg == regid2)
			if (freereg + 1 == regid || freereg + 1 == regid2)
				return freereg + 2;
			else
				return freereg + 1;
		return freereg;
	}

	public override function ToRegister(Compiler compiler, int32 dst) -> RegisterValue
	{
		compiler::AddCode("lde" + type::.getSuffix() + "\t$" + dst + ", $" + regid + "($" + regid2 + ")");
		return new RegisterValue(location, dst, type);
	}

	public override function AssignValue(Compiler compiler, Value value, int32 freereg)
	{
		RegisterValue v = value::EnsureTypedRegister(compiler, freereg, type);
		compiler::AddCode("lde" + type::.getSuffix() + "\t$" + regid + "($" + regid2 + "), $" + v::regid);
	}
}
